apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//Maven Variables
group = 'net.scarlettsystems.android'
version = project.property('widget_version')
archivesBaseName = 'widget'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName project.property('widget_version')
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-v13:27.0.2'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.6.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

bintray {
    user = properties.getProperty('bintray_user')
    key = properties.getProperty('bintray_apikey')
    configurations = ['archives']
    pkg {
        repo = 'net.scarlettsystems.android'
        name = 'widget'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/shanescarlett/net.scarlettsystems.android'
        version {
            name = project.property('widget_version')
            desc = project.property('widget_version_description')
            vcsTag = project.property('widget_version')
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            group 'net.scarlettsystems.android'
            artifactId 'widget'
            version project.property('widget_version')
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

artifacts {
    archives generateSourcesJar
}
